buildscript {
    repositories {
        maven { url "https://maven.fabricmc.net/" }
    }
}

plugins {
    id 'java'
    id 'cn.apisium.papershelled' version '1.0.3'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'cn.apisium.example'
version = System.getenv('RELEASE_VERSION') ?: "0.0.0-DEV"

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url 'https://www.jitpack.io' }
}

paperShelled {
    archiveClassifier = ''
    jarUrl = "https://papermc.io/api/v2/projects/paper/versions/1.17.1/builds/257/downloads/paper-1.17.1-257.jar"
}

dependencies {
    compileOnly paperShelled.jar()
    compileOnly 'org.spongepowered:mixin:0.8.4'
    compileOnly 'com.github.Apisium:PaperShelled:ddaf6a5d18'
    compileOnly 'com.github.Apisium:PaperShelledAnnotations:a7326507d3'
    compileOnly 'org.jetbrains:annotations:22.0.0'

    annotationProcessor 'com.github.Apisium:PaperShelledAnnotations:a7326507d3'
}

shadowJar {
    dependencies {
        relocate('org.bukkit.craftbukkit', 'org.bukkit.craftbukkit.' + paperShelled.craftBukkitVersion.get())
    }
    archiveClassifier.set('')
    minimize()

    filesMatching('plugin.yml') {
        filter {
            // noinspection GrDeprecatedAPIUsage
            it.replaceAll('@@RELEASE_VERSION@@', version)
        }
    }
}
build.dependsOn(shadowJar)
